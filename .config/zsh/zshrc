main() {
    apps_configure_envs
    apps_configure_thefuck
    shell_autoload_functions
    shell_completions
    shell_custom_aliases
    shell_custom_path
    shell_ignore_cli_comments
    shell_infinite_history
    shell_user_extensions
}

apps_configure_envs() {
    eval "$(mise activate zsh)"
    [[ -f $(brew --prefix)/etc/profile.d/autojump.sh ]] && source $(brew --prefix)/etc/profile.d/autojump.sh
}

apps_configure_thefuck() {
    eval $(thefuck --alias)
    export THEFUCK_REQUIRE_CONFIRMATION='false'
}

# [zsh: 17 Shell Builtin Commands#autoload](https://zsh.sourceforge.io/Doc/Release/Shell-Builtin-Commands.html#index-autoload)
# [zsh: 14 Expansion#Modifiers](https://zsh.sourceforge.io/Doc/Release/Expansion.html#Modifiers)
shell_autoload_functions() {
    fpath=(
        ~/.config/zsh/functions
        $(brew --prefix)/share/zsh-completions
        $fpath
    )
    autoload -Uz $fpath[1]/*(:t)
}

# https://docs.brew.sh/Shell-Completion#configuring-completions-in-zsh
shell_completions() {
    autoload -Uz bashcompinit && bashcompinit
    autoload -Uz compinit && compinit
}

shell_custom_aliases() {
    [[ -f $(get_script_dir)/aliases ]] && source $(get_script_dir)/aliases || :
}

shell_custom_path() {
    path=($LOCAL_SCRIPTS $LOCAL_BIN $path)
}

shell_ignore_cli_comments() {
    setopt INTERACTIVE_COMMENTS
}

# [zsh: 15 Parameters](https://zsh.sourceforge.io/Doc/Release/Parameters.html#Parameters-Used-By-The-Shell)
# [zsh: 16 Options#History](https://zsh.sourceforge.io/Doc/Release/Options.html#History)
shell_infinite_history() {
    # man zshparam
    export HISTSIZE=10000000
    export SAVEHIST=$HISTSIZE
    # man zshoptions
    setopt EXTENDED_HISTORY
    setopt HIST_FIND_NO_DUPS
    setopt HIST_IGNORE_DUPS
    setopt HIST_IGNORE_SPACE
    setopt HIST_VERIFY
    setopt SHARE_HISTORY    # https://apple.stackexchange.com/a/427568
}

shell_user_extensions() {
    [[ -f $(brew --prefix)/opt/spaceship/spaceship.zsh ]] && source $(brew --prefix)/opt/spaceship/spaceship.zsh
    [[ -f $(brew --prefix)/share/zsh-autosuggestions/zsh-autosuggestions.zsh ]] && source $(brew --prefix)/share/zsh-autosuggestions/zsh-autosuggestions.zsh
    [[ -f $(brew --prefix)/bin/fzf ]] && eval "$(fzf --zsh)"

    # [Must be the last one being sourced](https://github.com/zsh-users/zsh-syntax-highlighting#why-must-zsh-syntax-highlightingzsh-be-sourced-at-the-end-of-the-zshrc-file)
    [[ -f $(brew --prefix)/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]] && source $(brew --prefix)/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
}

main

# TODO: Move to work-specific repo and load from there
[[ -f "$HOME/custom-source.work.sh" ]] && source "$HOME/custom-source.work.sh" || :
